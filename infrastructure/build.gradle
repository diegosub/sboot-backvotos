buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.flywaydb.flyway' version '8.5.10'
}

group 'br.com.desafio.backvotos.infrastructure'
version '1.0-SNAPSHOT'

bootJar {
    archiveName 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation('org.postgresql:postgresql:42.5.0')

    implementation('org.springdoc:springdoc-openapi-webmvc-core:1.6.11')
    implementation('org.springdoc:springdoc-openapi-ui:1.6.11')

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.amqp:spring-rabbit-test')
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')
    implementation('org.flywaydb:flyway-core')

    testImplementation('org.flywaydb:flyway-core')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    testImplementation('org.testcontainers:testcontainers:1.17.3')
    testImplementation('org.testcontainers:postgresql:1.17.3')
    testImplementation('org.testcontainers:junit-jupiter:1.17.3')

    compileOnly('org.projectlombok:lombok:1.18.24')
    annotationProcessor('org.projectlombok:lombok:1.18.24')

    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:postgresql://localhost:5432/backvotos'
    user = System.getenv('FLYWAY_USER') ?: 'postgres'
    password = System.getenv('FLYWAY_PASS') ?: 'postgres'
}

test {
    useJUnitPlatform()
}